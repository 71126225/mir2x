FILE(GLOB FLTK_ALL_SRC "*.[fF][lL]")

SET(FLTK_CPP_SRC "")

FOREACH(FLTK_SRC ${FLTK_ALL_SRC})
    # generate fltk output source file name
    GET_FILENAME_COMPONENT(T_BASENAME ${FLTK_SRC} NAME_WE)
    SET(T_OUT_CPPNAME ${T_BASENAME}.cpp)
    SET(T_OUT_HPPNAME ${T_BASENAME}.hpp)

    SET(FLTK_CPP_SRC
        ${FLTK_CPP_SRC}
        ${CMAKE_CURRENT_BINARY_DIR}/${T_OUT_HPPNAME}
        ${CMAKE_CURRENT_BINARY_DIR}/${T_OUT_CPPNAME})

    ADD_CUSTOM_COMMAND(OUTPUT ${T_OUT_HPPNAME} ${T_OUT_CPPNAME}
        DEPENDS ${FLTK_SRC}
        COMMAND fluid -c -o ${T_OUT_CPPNAME} -h ${T_OUT_HPPNAME} ${FLTK_SRC}
        WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}
        )
ENDFOREACH()

AUX_SOURCE_DIRECTORY(. MONOSERVER_SRC)
ADD_EXECUTABLE(monoserver ${MONOSERVER_SRC} ${FLTK_CPP_SRC})

TARGET_INCLUDE_DIRECTORIES(monoserver PRIVATE ${COMMON_SOURCE_DIR})
TARGET_INCLUDE_DIRECTORIES(monoserver PRIVATE ${CMAKE_CURRENT_BINARY_DIR})
TARGET_INCLUDE_DIRECTORIES(monoserver PRIVATE ${CMAKE_CURRENT_LIST_DIR})

IF(MIR2X_ENABLE_MYSQL)
    FIND_PACKAGE(MySQL   QUIET)
    FIND_PACKAGE(MariaDB QUIET)

    IF(MARIADB_FOUND)
        ADD_DEFINITIONS(-DMIR2X_MYSQL_IMPL_MARIADB)
        LINK_DIRECTORIES(${MARIADB_LIBRARY_DIR})
        TARGET_LINK_LIBRARIES(monoserver ${MARIADB_LIBRARIES})
    ELSE()
        IF(MYSQL_FOUND)
            ADD_DEFINITIONS(-DMIR2X_MYSQL_IMPL_MYSQL)
            LINK_DIRECTORIES(${MYSQL_LIBRARY_DIR})
            TARGET_LINK_LIBRARIES(monoserver ${MYSQL_LIBRARIES})
        ELSE()
            MESSAGE(FATAL_ERROR "Can't find an implementation of MySQL.")
        ENDIF()
    ENDIF()
ENDIF()

TARGET_LINK_LIBRARIES(monoserver ${LUA_LIBRARIES}   )
TARGET_LINK_LIBRARIES(monoserver ${FLTK_LIBRARIES}  )
TARGET_LINK_LIBRARIES(monoserver ${OPENGL_LIBRARIES})
TARGET_LINK_LIBRARIES(monoserver ${THERON_LIBRARIES})
TARGET_LINK_LIBRARIES(monoserver dl                 )
TARGET_LINK_LIBRARIES(monoserver common             )
TARGET_LINK_LIBRARIES(monoserver pthread            )
TARGET_LINK_LIBRARIES(monoserver stdc++fs           )
TARGET_LINK_LIBRARIES(monoserver ${G3LOG_LIBRARIES} )
TARGET_LINK_LIBRARIES(monoserver ${ZIP_LIBRARIES}   )
TARGET_LINK_LIBRARIES(monoserver ${ZLIB_LIBRARIES}  )
TARGET_LINK_LIBRARIES(monoserver ${ZSTD_LIBRARIES}  )

INSTALL(TARGETS monoserver DESTINATION server)
INSTALL(DIRECTORY ${CMAKE_SOURCE_DIR}/server/monoserver/script DESTINATION server)
INSTALL(DIRECTORY ${CMAKE_SOURCE_DIR}/client/bin/Res/Map DESTINATION server)
